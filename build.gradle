plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.14'
}

version = '1.21.1-8.0.0'
group = 'com.blakebr0.mysticalagradditions'

base {
    archivesName = 'MysticalAgradditions'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

boolean hasCucumber = findProject(':Cucumber') != null
boolean hasMysticalAgriculture = findProject(':MysticalAgriculture') != null

repositories {
    maven { url 'https://maven.blakesmods.com' }
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven {
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

neoForge {
    version = neo_version

    parchment {
        mappingsVersion = parchment_mappings_version
        minecraftVersion = parchment_minecraft_version
    }

    runs {
        client {
            client()
        }

        server {
            server()
        }

        gameTestServer {
            type = "gameTestServer"
        }

        data {
            data()

            // programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        mysticalagradditions {
            sourceSet(sourceSets.main)
        }

        if (hasCucumber) {
            cucumber {
                sourceSet(project(':Cucumber').sourceSets.main)
            }
        }

        if (hasMysticalAgriculture) {
            evaluationDependsOn(':MysticalAgriculture')

            mysticalagriculture {
                sourceSet(project(':MysticalAgriculture').sourceSets.main)
            }
        }
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    if (hasCucumber) {
        implementation(project(':Cucumber')) {
            transitive = false
        }
    } else {
        implementation "com.blakebr0.cucumber:Cucumber:${cucumber_version}"
    }

    if (hasMysticalAgriculture) {
        implementation(project(':MysticalAgriculture')) {
            transitive = false
        }
    } else {
        implementation "com.blakebr0.mysticalagriculture:MysticalAgriculture:${mysticalagriculture_version}"
    }

    implementation "curse.maven:jade-324717:${jade_version}"
    compileOnly 'slimeknights.tconstruct:TConstruct:1.18.2-3.5.1.31' // TODO: implementation
    compileOnly 'slimeknights.mantle:Mantle:1.18.2-1.9.27' // TODO: implementation

    runtimeOnly "vazkii.patchouli:Patchouli:${patchouli_version}"
    runtimeOnly "mezz.jei:jei-${jei_mc_version}-neoforge:${jei_version}"

    compileOnly "mezz.jei:jei-${jei_mc_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${jei_mc_version}-neoforge-api:${jei_version}"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            mod_version            : version.split('-').last(),
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://maven.blakesmods.com'

            credentials {
                username = System.getenv('BLAKESMODS_MAVEN_USERNAME')
                password = System.getenv('BLAKESMODS_MAVEN_PASSWORD')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}